#ifndef DIALOG_MEDIA_GUIDE_DESCRIPTION_H
#define DIALOG_MEDIA_GUIDE_DESCRIPTION_H
#include "credits.qc"
CLASS(XonoticGuideDescription, XonoticListBox)
	ATTRIB(XonoticGuideDescription, rowsPerItem, float, 1)
	ATTRIB(XonoticGuideDescription, selectionDoesntMatter, bool, true)

    METHOD(XonoticGuideDescription, setDescription, void(entity, string));
    ATTRIB(XonoticGuideDescription, description, string, string_null)

	METHOD(XonoticGuideDescription, resizeNotify, void(entity this, vector relOrigin, vector relSize, vector absOrigin, vector absSize)) {
        super.resizeNotify(this, relOrigin, relSize, absOrigin, absSize);

        this.realFontSize_y = this.fontSize / (absSize.y * this.itemHeight);
        this.realFontSize_x = this.fontSize / (absSize.x * (1 - this.controlWidth));
        this.realUpperMargin = 0.5 * (1 - this.realFontSize.y);
        this.setDescription(this, this.description);
    }

    INIT(XonoticGuideDescription) {
        this.configureXonoticListBox(this);
    }

    ATTRIB(XonoticGuideDescription, descriptionWrapped, string, string_null)
    void XonoticGuideDescription_setDescription(entity this, string desc)
    {
        string current = this.description;
        if (current && current != desc) strunzone(current);
        this.description = strzone(desc);

        string currentWrapped = this.descriptionWrapped;
        if (currentWrapped) strunzone(currentWrapped);
        string wrapped = "";
        for (int i = 0, n = tokenizebyseparator(desc, "\n"); i < n; ++i) {
            string line = "";
            for (getWrappedLine_remaining = argv(i); getWrappedLine_remaining; ) {
                string s = getWrappedLine(1, this.realFontSize, draw_TextWidth_WithColors);
                line = sprintf("%s\n%s", line, s);
            }
            wrapped = strcat(wrapped, line);
        }
        this.descriptionWrapped = strzone(wrapped);

        this.nItems = tokenizebyseparator(wrapped, "\n");
    }

    METHOD(XonoticGuideDescription, drawListBoxItem, void(entity this, int i, vector absSize, bool isSelected, bool isFocused)) {
        tokenizebyseparator(this.descriptionWrapped, "\n");
        draw_Text(this.realUpperMargin * eY, argv(i), this.realFontSize, '1 1 1', 1, 0);
    }
ENDCLASS(XonoticGuideDescription)
#endif
